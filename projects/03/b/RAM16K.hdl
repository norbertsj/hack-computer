// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/b/RAM16K.hdl

/**
 * Memory of 16K registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then
 * the in value is loaded into the memory location specified by address
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM16K {
    IN in[16], load, address[14];
    OUT out[16];

    PARTS:
    // This chip consists of 4xRAM4K chips
    // addr[0..11] is used as internal 12-bit address for all RAM4K chips
    // addr[12..13] is used to select which RAM4K to work with (load input and/or output)

    // Select one of the RAM4K chips (for input)
    DMux4Way(in=load, sel=address[12..13], a=ram1, b=ram2, c=ram3, d=ram4);

    // All RAM4K chips are reading the same internal address
    RAM4K(in=in, load=ram1, address=address[0..11], out=out1);
    RAM4K(in=in, load=ram2, address=address[0..11], out=out2);
    RAM4K(in=in, load=ram3, address=address[0..11], out=out3);
    RAM4K(in=in, load=ram4, address=address[0..11], out=out4);

    // Select one the RAM4K chips (for read)
    Mux4Way16(a=out1, b=out2, c=out3, d=out4, sel=address[12..13], out=out);
}