// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input
 * holds the value of this location. If the current instruction needs
 * to write a value to M, the value is placed in outM, the address
 * of the target location is placed in the addressM output, and the
 * writeM control bit is asserted. (When writeM==0, any value may
 * appear in outM). The outM and writeM outputs are combinational:
 * they are affected instantaneously by the execution of the current
 * instruction. The addressM and pc outputs are clocked: although they
 * are affected by the execution of the current instruction, they commit
 * to their new values only in the next time step. If reset==1 then the
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather
 * than to the address resulting from executing the current instruction.
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M?
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

    // Op code selector
    Mux16(a=instruction, b=aluOut, sel=instruction[15], out=inA); // instruction[15] => 0 = A, 1 = C

    // A register
    Not(in=instruction[15], out=notInstr15);
    Or(a=instruction[5], b=notInstr15, out=loadA); // loadA = d1 OR NOT(instruction[15])
    ARegister(in=inA, load=loadA, out=outA, out[0..14]=addressM);

    // D register
    And(a=instruction[4], b=instruction[15], out=loadD); // loadD = d2 AND instruction[15]
    DRegister(in=aluOut, load=loadD, out=outD);

    // writeM
    And(a=instruction[3], b=instruction[15], out=writeM); // writeM = d3 AND instruction[15]

    // M/A selector
    Mux16(a=outA, b=inM, sel=instruction[12], out=outAM);

    // ALU
    ALU(x=outD, y=outAM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluOut, out=outM, zr=aluZr, ng=aluNg);

    // PC logic (using custom Mux8Way, there probably is a better solution)
    Not(in=aluZr, out=notAluZr);
    Not(in=aluNg, out=notAluNg);
    And(a=notAluZr, b=notAluNg, out=pcB);
    Or(a=aluZr, b=notAluNg, out=pcD);
    And(a=notAluZr, b=aluNg, out=pcE);
    Or(a=aluZr, b=aluNg, out=pcG);
    Mux8Way(a=false, b=pcB, c=aluZr, d=pcD, e=pcE, f=notAluZr, g=pcG, h=true, sel=instruction[0..2], out=pcMuxLoad);
    And(a=pcMuxLoad, b=instruction[15], out=pcLoad);
    Not(in=pcLoad, out=pcInc);
    PC(in=outA, load=pcLoad, inc=pcInc, reset=reset, out[0..14]=pc);
}